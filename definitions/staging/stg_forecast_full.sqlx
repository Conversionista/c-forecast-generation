config {
    type: "table",
    schema: "c_forecast_dev",
    name: "stg_harvest_forecast",
    tags: ['Sweden', 'Norway']
}

WITH Assignments AS (
  SELECT
    id,
    CAST(start_date AS DATE) AS start_date,
    CAST(end_date AS DATE) AS end_date,
    allocation,
    project_id,
    person_id,
    updated_at
  FROM ${ref("stg_assignments_combined_filtered")}
),
ProjectDetails AS (
  SELECT
    t1.*,
    COALESCE(t2.name, t3.name) AS client,
    COALESCE(t2.harvest_id, t3.harvest_id) AS harvest_client_id
  FROM ${ref("stg_projects_combined_filtered")} AS t1
  LEFT JOIN ${ref("forecast_se_clients")} AS t2
    ON CAST(t1.client_id AS STRING) = CAST(t2.id AS STRING)
  LEFT JOIN ${ref("forecast_no_clients")} AS t3
    ON CAST(t1.client_id AS STRING) = CAST(t3.id AS STRING)
),
EmployeeInfo AS (
  SELECT *
  FROM ${ref("stg_people_combined")}
)

SELECT
  a.id AS task_id,
  a.start_date,
  a.end_date,
  (a.allocation/3600) AS hours_per_day,
  (CASE
      WHEN DATE_DIFF(a.end_date, a.start_date, WEEK) > 0 THEN DATE_DIFF(a.end_date, a.start_date, DAY) - (DATE_DIFF(a.end_date, a.start_date, WEEK) * 2) + 1
      ELSE DATE_DIFF(a.end_date, a.start_date, DAY) + 1
  END) * (a.allocation/3600) AS total_hours,
  a.updated_at,
  e.name,
  e.employee_id,
  e.country,
  e.company,
  e.level, 
  e.location,
  e.track,
  e.specialty,
  e.seniority,
  p.client,
  p.name AS project_name,
  p.code AS project_code,
  LEFT(p.code, 4) AS client_id,
  p.color,
  p.start_date AS project_start_date,
  p.end_date AS project_end_date,
  p.archived,
  CASE
    WHEN a.start_date <= CURRENT_DATE() AND a.end_date > CURRENT_DATE() THEN "Yes"
    ELSE "No"
  END AS active
FROM Assignments AS a
LEFT JOIN EmployeeInfo AS e
  ON a.person_id = e.forecast_person_id
LEFT JOIN ProjectDetails AS p
  ON a.project_id = p.id