config {
    type: "table",
    schema: "c_forecast_dev",
    name: "int_forecast_daily"
}

js {
    const yearly_holidays = dataform.projectConfig.vars.yearly_holidays;
    const specific_holidays = dataform.projectConfig.vars.specific_holidays;
}

WITH yearly_holidays AS (
  SELECT holiday FROM UNNEST(${yearly_holidays}) AS holiday
),
specific_holidays AS (
  SELECT CAST(holiday AS DATE) AS holiday FROM UNNEST(${specific_holidays}) AS holiday
),
dates AS (
  SELECT *
  FROM (
    SELECT DISTINCT date,
    CAST(FORMAT_DATE("%u", CAST(date AS DATE)) AS INT64) AS day_of_week,
    "1" AS merge_id 
    FROM `bigquery-public-data.utility_eu.date_greg`
    WHERE date BETWEEN '2020-09-01' AND DATE_ADD(CURRENT_DATE(), INTERVAL 180 DAY)
    ORDER BY DATE ASC
  )
  WHERE day_of_week < 6 
    AND SUBSTR(CAST(date AS STRING), 6, 5) NOT IN (SELECT holiday FROM yearly_holidays)
    AND date NOT IN (SELECT holiday FROM specific_holidays)
), 
harvest_forecast AS (
  SELECT 
    *,
    "1" AS merge_id 
  FROM (
    SELECT
      task_id,
      CASE
        WHEN project_name = "Time Off" THEN 8
        ELSE hours_per_day
      END AS hours_per_day,
      updated_at,
      name,
      employee_id,
      company,
      track,
      level,
      seniority,
      client,
      project_name,
      project_code,
      client_id,
      color,
      project_start_date,
      project_end_date,
      start_date,
      end_date,
      archived
    FROM ${ref("int_harvest_forecast")} 
    GROUP BY 1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19
  )
)

SELECT t1.date, t2.* EXCEPT(merge_id)
FROM dates t1
LEFT JOIN harvest_forecast AS t2 USING(merge_id)
WHERE t1.date BETWEEN t2.start_date AND t2.end_date
ORDER BY task_id, t1.date ASC